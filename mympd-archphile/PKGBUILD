# Maintainer: Mike Andonov <info@archphile.org>
# Website: http://archphile.org
# Based on ympd-git from AUR made by the following people:
# Maintainer: Philippe Mongeau <ph.mongeau@gmail.com>

pkgname=mympd-archphile
_pkgname=myMPD
pkgver=3.2.1.r1.gd2438d9
pkgrel=1
pkgdesc="A standalone MPD Web GUI based on YMPD - Default port set to 80"
arch=('x86_64' 'armv7h' 'aarch64')
url="http://github.org/jcorporation"
license=('BSD')
depends=('libmpdclient' 'openssl')
makedepends=('cmake' 'git')
optdepends=()
provides=()
conflicts=()
replaces=()
install=mympd-archphile.install
source=("git+https://github.com/jcorporation/myMPD.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tags | sed -E 's/^v//; s/([^-]*-g)/r\1/; s/-/./g'
}

build() {
  cd "${srcdir}/${_pkgname}"

 
  mkdir release && cd release
  cmake .. -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_BUILD_TYPE=RELEASE ..
  make
}

package() {
  cd "${srcdir}/${_pkgname}/release"
  make DESTDIR="$pkgdir/" install
  
  install -Dm644  "${srcdir}/${_pkgname}/contrib/mympd.service" "$pkgdir/usr/lib/systemd/system/mympd.service"
  install -Dm644  "${srcdir}/${_pkgname}/contrib/options" "$pkgdir/etc/mympd/options"
  
  # Replacing javascript and stylesheets with minified files
  sed -i -e 's/mpd\.css/mpd\.min\.css/' -e 's/mpd\.js/mpd\.min\.js/' $pkgdir/usr/share/mympd/htdocs/index.html
  sed -i -e 's/mpd\.css/mpd\.min\.css/' -e 's/mpd\.js/mpd\.min\.js/' -e 's/player\.js/player\.min\.js/' $pkgdir/usr/share/mympd/htdocs/sw.min.js
  sed -i -e 's/\/sw\.js/\/sw\.min\.js/' $pkgdir/usr/share/mympd/htdocs/js/mpd.min.js
  
  # Minifying html
  perl -i -pe 's/^\s*//gm; s/\s*$//gm' $pkgdir/usr/share/mympd/htdocs/index.html
  perl -i -pe 's/^\s*//gm; s/\s*$//gm' $pkgdir/usr/share/mympd/htdocs/player.html
}

