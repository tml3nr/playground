# Maintainer: Tux <info@archphile.org>
# Website: http://archphile.org
# Based on squeezelite-git from AUR made by the following people:
# IanDury

pkgname=squeezelite-archphile
pkgver=v1.9.0_1098.b2ed99e
pkgrel=2
pkgdesc='Lightweight headless squeezebox emulator with Native DSD patch'
arch=('armv7h' 'aarch64')
url='https://github.com/ralph-irving/squeezelite'
license=(GPL3)
makedepends=('ffmpeg-archphile')
depends=('alsa-lib' 'faad2' 'flac' 'libmad' 'libvorbis' 'mpg123' 'libsoxr' 'ffmpeg-archphile')
provides=('squeezelite')
conflicts=('squeezelite')
source=('git+https://github.com/ralph-irving/squeezelite'
	'service'
	'conffile'
	'install'
	)
sha256sums=('SKIP'
            '6f9930e9f67ceac64f93dc6fea06728e1eba7cb3ccac167feb0bf1823a88dd3c'
            '136d358d90b696ab696e284b2c1093ab104f5a41058277c249880f5b3d3bfa81'
            '2c52ea3b2916f9d67e3029506eabba2dabb3193c2833bcc7d2ff268d3a9ab5b9')

install=install

pkgver() {
	cd squeezelite

	# printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	printf "%s.%s" "$(cat squeezelite.h|grep VERSION | awk '{print $3}' | sed 's/\"//g;s/-/_/g')" "$(git rev-parse --short HEAD)"
}

build() {
	cd squeezelite
#	cat ./scripts/squeezelite-ralphy-dsd.patch | patch -p 0
	export LDFLAGS="${LDFLAGS} -lasound -lpthread -lm -lrt"
	export OPTS="${OPTS} -DDSD -DRESAMPLE -DVISEXPORT -DFFMPEG -DLINKALL"
	make
}

package() {
	cd squeezelite

	# Main binary
	install -m0755 -D squeezelite "${pkgdir}/usr/bin/squeezelite"
	install -Dm644 ../conffile "${pkgdir}/etc/squeezelite.conf"
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/squeezelite.service"
	install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/squeezelite.LICENSE
}




